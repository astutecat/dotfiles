# Execute code that does not affect the current session in the background.
{
  function  zcompare() {
    if [[ -s ${1} && ( ! -s "${1}.zwc" || ${1} -nt "${1}.zwc") ]]; then
      zcompile ${1}
    fi
  }

  # Compile the completion dump to increase startup speed.
  zcompdump="${XDG_CACHE_HOME:-$HOME/.cache}/prezto/zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    if command mkdir "${zcompdump}.zwc.lock" 2>/dev/null; then
      zcompile "$zcompdump"
      command rmdir  "${zcompdump}.zwc.lock" 2>/dev/null
    fi
  fi

  for file in ${ZDOTDIR:-$HOME}/*.zsh; do
      zcompare ${file}
  done

  for f in ${ZDOTDIR:-$HOME}/.zfunc/*; do
    case $f in
      *.zwc)
        continue
        ;;
      *)
        zcompare $f
        ;;
    esac
  done

  zcompare "${ZDOTDIR:-$HOME}/.zshrc"
  zcompare "${ZDOTDIR:-$HOME}/.zprofile"

} &!
